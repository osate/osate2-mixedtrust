-- Mixed-Trust Scheduling Analysis OSATE Plugin
--
-- Copyright 2021 Carnegie Mellon University.
--
-- NO WARRANTY. THIS CARNEGIE MELLON UNIVERSITY AND SOFTWARE ENGINEERING
-- INSTITUTE MATERIAL IS FURNISHED ON AN "AS-IS" BASIS. CARNEGIE MELLON
-- UNIVERSITY MAKES NO WARRANTIES OF ANY KIND, EITHER EXPRESSED OR IMPLIED,
-- AS TO ANY MATTER INCLUDING, BUT NOT LIMITED TO, WARRANTY OF FITNESS FOR
-- PURPOSE OR MERCHANTABILITY, EXCLUSIVITY, OR RESULTS OBTAINED FROM USE OF
-- THE MATERIAL. CARNEGIE MELLON UNIVERSITY DOES NOT MAKE ANY WARRANTY OF
-- ANY KIND WITH RESPECT TO FREEDOM FROM PATENT, TRADEMARK, OR COPYRIGHT
-- INFRINGEMENT.
--
-- Released under the Eclipse Public License - v 2.0 license, please see
-- license.txt or contact permission@sei.cmu.edu for full terms.
--
-- [DISTRIBUTION STATEMENT A] This material has been approved for public
-- release and unlimited distribution.  Please see Copyright notice for
-- non-US Government use and distribution.
--
-- Carnegie MellonÂ® is registered in the U.S. Patent and Trademark Office
-- by Carnegie Mellon University.
--
-- DM21-0927

-- @codegen-package org.osate.analysis.mixedtrust.contribution.mixedtrustproperties
property set Mixed_Trust_Properties is
	Mixed_Trust_Task: type record (
		-- A task name to improve readability of the results
		Name: aadlstring;
		
		-- Period of the mixed trust task.  Periods of the referenced Guest and Hyper task threads should not be set
		Period: Time;
		-- Deadline of the mixed trust task.  Periods of the referenced Guest and Hyper task threads should not be set
		Deadline: Time;
		
		-- The Guest and Hyper task threads should be bound to different virtual processors representing
		-- the guest OS and hypervisor environments.  These virtual processors, in turn, must be bound to the
		-- physical processor;  
		
		-- Reference to the guest task thread.  Thread should only be referenced by a single mixed trust task
		GuestTask: reference (Thread);
		-- Reference to the hyper task thread.  Thread should only be referenced by a single mixed trust task
		HyperTask: reference (Thread);	
		
		-- The "E" Value of the mixed trust task.  Effectively the deadline of the guest task, but also
		-- duration of the time used in the hypervisor used to activate the hypertask if the guest task
		-- does not meet its deadline.
		E: Time;
	);
	
	Mixed_Trust_Tasks: list of Mixed_Trust_Properties::Mixed_Trust_Task applies to (System);
	
	
	
	-- Specifies the exact virtual processors that represent the Guest OS and hypervisor of a processor suited by
	-- mixed trust scheduling.  Although AADL syntax does not require both fields of this record to have values,
	-- we require that both fields be specified, and that furthermore, the referenced virtual processors must be 
	-- bound to the same (physical) processor.
	Mixed_Trust_Bindings: type record (
		GuestOS: reference (virtual processor);
		HyperVisor: reference (virtual processor);
	);
	
	-- Used to mark a processor as being a mixed trust execution environment. Specifies the exact virtual processors
	-- that represent the Guest OS and hypervisor.  The processor must have exactly two virtual processors bound to it,
	-- via Actual_Processor_Binding, and they must be these two.  Furthermore, both fields of the Mixed_Trust_Bindings
	-- record must be set.
	Mixed_Trust_Processor: Mixed_Trust_Properties::Mixed_Trust_Bindings applies to (processor);
end Mixed_Trust_Properties;
